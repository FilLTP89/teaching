% [][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][]
%     Computer Modern Math Blackboard Sans Serif Special
%     PROGRAM FILE d'apres romanl.mf
% [][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][]
%     DUREISSEIX David    L.M.T. STRUCTURES et CMAO  le 10 / 09 / 95
%     DUREISSEIX David    L.M.T. STRUCTURES et CMAO  le 07 / 08 / 96
%     DUREISSEIX David    L.M.T. STRUCTURES et CMAO  le 11 / 08 / 96
%
%     Quelques lettres mathrm  en Blackboard Bold
%     a partir de Computer Modern Roman et
%     des fontes bbm
%
%     Attention : cette redefinition est tres laide.
% ======================================================================
%
cmchar "The letter k";
%       """"""""""""
%           12   1 
%
%           13   0
%
%           14   0'
%    
%           15   2 
%
beginchar(oct"153",9.5u#,asc_height#,0);
italcorr x_height#*slant-.2u#;
adjust_fit(serif_fit#,serif_fit#); pickup tiny.nib;
numeric right_jut,stem[],alpha[];
stem1=max(tiny.breadth,hround(fudged.stem-stem_corr));
stem2=max(tiny.breadth,hround(fudged.stem-2stem_corr));
stem3=max(tiny.breadth,hround(fudged.hair if hefty:-\\4stem_corr fi));
stem4=max(tiny.breadth,hround(fudged.stem-3stem_corr));
if serifs: right_jut=.6jut; else: right_jut=.4tiny; fi
%pos1(stem1,0); pos2(stem2,0);
pos1(vair,0); pos2(vair,0); pos12(vair,0); pos15(vair,0);
top y1=h; bot y2=0;
lft x1l=lft x2l=hround(2.5u-.5stem1);
top y3=x_height; rt x3r=hround(r-letter_fit-.7u-right_jut)+eps;
bot y6=0; rt x6r=hround(r-letter_fit-.3u-right_jut)+eps;
x4=x11=x1;
y4=.7bar_height;
y11=y3;
alpha1=diag_ratio(1,.5(stem3-tiny),y3-y4,x3r-x4);
alpha2=diag_ratio(1,.5(stem4-tiny),y11-y6,x6r-x1);
penpos3(alpha1*(stem3-tiny),0); penpos4(whatever,-90);
z5=.5[z5l,z5r]; penpos6(alpha2*(stem4-tiny),0);
forsuffixes $=l,r: y3'$=x_height; y6'$=0; z4$=z3'$+whatever*(z3-z4);
 z5$=z6'$+whatever*(z11-z6)=whatever[z3,z4]; endfor
z3'r=z3r+penoffset z3-z4 of currentpen+whatever*(z3-z4);
% we have also |z3'l=z3l+penoffset z4-z3 of currentpen+whatever*(z3-z4)|;\]
z6'r=z6r+penoffset z11-z6 of currentpen+whatever*(z11-z6);
z6'l=z6l+penoffset z6-z11 of currentpen+whatever*(z11-z6);
fill z4r--diag_end(4r,3'r,1,.5,3'l,4l)--z4l--cycle;  % upper diagonal
fill z5l--diag_end(5l,6'l,.5,1,6'r,5r)--z5r--cycle;  % lower diagonal
%pos0(stem1,0); pos0'(stem2,0); y0=y0'; x0=x1; x0'=x2;
pos0(vair,0); pos0'(vair,0); y0=y0'; x0=x1; x0'=x2;
pos13(vair,0); pos14(vair,0);
rt z0r=whatever[z3,z4];
filldraw stroke z1e..z0e--z0'e..z2e; % stem
x12=x1-2u; y12=y1; x13=x0-2u; y13=y0; x14=x0'-2u; y14=y0'; x15=x2-2u; y15=y2;
filldraw stroke z12e..z13e--z14e..z15e; % stem
filldraw stroke z1e..z12e; filldraw stroke z2e..z15e; % bars
if serifs: numeric inner_jut;
 if rt x2r+jut+.5u+1<=lft x6l-jut: inner_jut=jut;
 else: rt x2r+inner_jut+.5u+1=lft x6l-inner_jut; fi
% sloped_serif.l(1,0,a,1/3,jut,serif_drop); % upper stem serif
 sloped_serif.l(12,13,a,1/3,jut,serif_drop); % upper stem serif
% dish_serif(2,0',b,1/3,jut,c,1/3,inner_jut);  % lower stem serif
 dish_serif(15,14,b,1/3,jut,c,1/3,inner_jut);  % lower stem serif
 dish_serif(3,4,d,2/3,1.4jut,e,1/2,right_jut)(dark); % upper diagonal serif
 dish_serif(6,5,f,1/2,inner_jut,g,1/3,right_jut)(dark);fi % lower diagonal serif
penlabels(0,1,2,3,4,5,6,11,12,13,14,15); endchar;

cmchar "The letter a";
%       """"""""""""
%
%                  3
%
%                      4  14
%
%
%
%                      5  15
%
%                      5' 15'
%                                 13
%                      10 16      12
%
%                           11
%
%beginchar("a",9u#,x_height#,0);
beginchar("a",10u#,x_height#,0);
bh#:=min(bar_height#,1.14x_height#-bar_height#);
define_pixels(bh);
italcorr 1/3[bh#,x_height#]*slant+.5stem#-serif_fit#-2u#;
adjust_fit(0,serif_fit# if serifs: if hair#+.5stem#>1.5u#:-.25u# fi\\fi);
pickup fine.nib;
top y3r=h+vround 1.5oo;
if serifs: pos1(flare,180);
pos2(hair,180);
 pos3(vair,90);
 lft x1r=hround max(u,2.1u-.5flare);
 x3=.5w-.5u;
 y1=min(bh+.5flare+2vair+2,.9[bh,h]-.5flare);
 bulb(3,2,1);  % bulb
else: pos1(5/7[vair,flare],95);
x1l=good.x 1.5u;
x1r:=good.x x1r;
 pos3(1/8[vair,thin_join],90);
 x3=.5w-.2u;
 top y1r=vround .82[bh,top y3r];
 filldraw stroke term.e(3,1,left,.9,4);
fi  % terminal
%pos4(stem,0);
pos4(vair,0);
%rt x4r=hround(w-2.5u+.5stem);
%y4=1/3[bh,h];
rt x4r=hround(w-3.5u+.5stem);
y4=1/3[bh,h]-0.2y4;
%%
pos14(vair,0);
x14=x4+2.u;
y14=y4;
%%
%pos5(stem,0);
pos5(vair,0);
x5=x4;
y5=max(.55bh,2vair);
%%
pos15(vair,0);
x15=x5+2.u;
y15=y5;
%%
filldraw stroke super_arc.e(3,4)&z4e..z5e;  % arc and stem
%%
filldraw stroke super_arc.e(3,14)&z14e..z15e;  % arc and stem dd
%%
pos6(.3[thin_join,vair],90);
x6=x4;
bot y6=bh;
pos7(hround(curve-2stem_corr),180);
lft x7r=hround max(.5u,1.5u-.5curve);
y7=1/3[top y8l,top y6r];
pos8(vair,270);
x8l=.5w-.75u;
bot y8r=-oo;
pos9(thin_join,360);
z9l=z5l;
(x,y8r)=whatever[z8l,z9l];
x8r:=max(x,x8-u);
{{interim superness:=more_super;
 filldraw stroke z9e{down}...z8e{left}...{up}z7e&super_arc.e(7,6)}}; % bowl
if serifs: numeric shaved_stem;
shaved_stem=hround(stem-3stem_corr);
 if hair#+.5stem#>1.5u#: pickup tiny.nib;
  pos5'(shaved_stem,0);
  rt x5'r=fine.rt x5r;
  y5'=y5;
  pos10(shaved_stem,0);
  x10=x5';
  y10=.2[.5tiny,bh];
  pos11(shaved_stem,0);
  rt x11r=hround(w-.25u);
  bot y11=0;
  pos12(shaved_stem,0);
  x11=x12;
  top y12=slab+eps;
  filldraw z5'l---z10l...z11l{right}--z11r
   --z12r{left}...z10r+.75(z12-z11)---z5'r--cycle;  % foot
 else: pickup crisp.nib;
% pos5'(shaved_stem,0);
 pos5'(vair,0);
 rt x5'r=fine.rt x5r;
 y5'=y5;
%%
 pos15'(vair,0);
 x15'=x5'+2.u;
 y15'=y5';
%%
%  pos10(shaved_stem,0);
  pos10(vair,0);
  x10=x5';
  y10=1/3bh;
%%
  pos16(vair,0);
  x16=x10+2.u;
  y16=y10;
%%
%  pos11(.2[vair,stem],90);
  pos11(vair,90);
%  x11r=.5[x10r,x12r];
  x11r=.5[x10r,x12r]+0.1x11;
  bot y11l=-vround .5oo;
  pos12(hair,180);
%  rt x12l=hround(w-.1u);
  rt x12l=hround(w+0.9u);
  y12=max(y10,y11+vair);
  pos13(hair,180);
  x13=x12;
  top y13=max(vround .6bh,top y12);
  (x',y11l)=whatever[z11r,z12r];
  x11l:=max(x',x10);
  filldraw stroke z5'e---z10e...z11e{right}...z12e---z13e; % hook
%%
  filldraw stroke z15'e---z16e...z11e{right}...z12e---z13e; % hook dd
%%
 fi
else: numeric shaved_stem;
shaved_stem=hround(stem-stem_corr);
 pickup tiny.nib;
 pos5'(shaved_stem,0);
 rt x5'r=fine.rt x5r;
 y5'=y5;
 pos10(shaved_stem,0);
 x10=x5';
 bot y10=0;
 filldraw stroke z5'e--z10e;
 fi  % base of stem
penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

iff not variant_g: cmchar "The letter g";
%                          """"""""""""
%
%                        1
%
%                 2 15
%
%                        3
%
beginchar("g",9u#,x_height#,desc_depth#);
italcorr x_height#*slant+.25u#;
adjust_fit(0,0);
numeric light_vair,light_curve,loop_top,loop_side;
light_vair=Vround(.5[thin_join,vair]+vair_corr);
light_curve=max(fine.breadth,hround(curve-3stem_corr));
loop_top=Vround .77[vair,fudged.stem];
loop_side=hround .64[vair,fudged.stem];
pickup fine.nib;
pos1(light_vair,90);
%pos2(light_curve,180);
pos2(vair,180);
pos3(light_vair,270);
pos4(light_curve,360);
pos11(loop_top,90);
y11r=good.y(y11r+.15bar_height-y11);
x1=x3=.5[x2,x4];
y2=y4=.5[y1,y3];
%lft x2r=hround(1.75u-.5light_curve);
lft x2r=hround(1.u-.5light_curve);
rt x4r=hround(w-2.75u+.5light_curve);
top y1r=h+oo;
y3r=good.y(y3r+.27[top y11r,bot y1l]-y3);
%%
pos15(vair,180);
x15=x2+2.u;
y15=y2;
filldraw stroke pulled_arc.e(1,15) & pulled_arc.e(15,3);  % left half of bowldd
%%
filldraw stroke pulled_arc.e(1,2) & pulled_arc.e(2,3);  % left half of bowl
filldraw stroke pulled_arc.e(3,4) & pulled_arc.e(4,1);  % right half of bowl
x0=superness[x1r,x4r];
y0=superness[y4r,y1r];  % NE point on the super bowl
x8'=superness[x3r,x2r];
y8'=superness[y2r,y3r];  % SW point on the super bowl
x8''=superness[x3,x2];
y8''=superness[y2,y3];
pos0(thin_join,angle(z0-z8')+90);
pos8(thin_join,angle(z0-z8')-90);
z8=.618[z8',z8''];
if serifs: pos5(vair,90);
 pos6(hair,0);
 pos7(.5[hair,flare],0);
 x5=.75[x0,x6];
 top y5r=h+o;
 rt x6r=hround(w-.25u);
 y6+(.5[hair,flare])/2=.95[bar_height,h]+oo;
 filldraw stroke z0e..{right}z5e;
 bulb(5,6,7);  % ear
else: pos5(vair',100);
top y5r=h+oo;
 rt x5l=hround(w-.25u);
 y5l:=good.y y5l;
 filldraw z0l{z5-z0}..z5l--z5r{left}..{curl 1}z0r--cycle;
 fi  % ear
pos10(loop_top,90);
x10=x8+.75u;
y10=y11;
pos12(loop_side,0);
pos13(light_vair,-90);
pos14(loop_side,-180);
pos10'(.5[thin_join,vair],-270);
rt x12r=hround max(w-1.25u+.5loop_side,w-.5u);
y12=y14=.5[y11,y13];
x11=x13=max(.5w,x10+eps);
bot y13r=-d-oo-1;
x14=w-x12;
z10'l=z10l;
filldraw stroke z8e{z8-z0}..z10e---z11e;  % link
filldraw stroke {{interim superness:=hein_super;
super_arc.e(11,12)}}
 & super_arc.e(12,13) & super_arc.e(13,14) & super_arc.e(14,10');  % loop
%penlabels(0,1,2,3,4,5,6,7,8,10,11,12,13,14);
penlabels(0,1,2,3,4,5,6,7,8,10,11,12,13,14,15);
labels(8',8'');
endchar;

iff variant_g: cmchar "Variant letter g";
%                      """"""""""""""""
%
%                    4
%                             3
%
%             5   12
%
%                              7
%                    6
%
beginchar("g",10u#+serif_fit#,x_height#,desc_depth#);
italcorr x_height#*slant-serif_fit#+.5stem#-2u# if serifs:+.5u# fi;
adjust_fit(0,serif_fit# if serifs: -.5u# fi);
pickup tiny.nib;
pos1(stem',0);
pos2(stem,0);
pos0'(stem',0);
pos0(stem,0);
z0r=z0'r;
x0'=x1;
x0=x2;
rt x1r=hround(w-2.5u+.5stem');
numeric edge;
edge=lft x2l;
path edge_path;
edge_path=(edge,h)--(edge,0);
pickup fine.nib;
pos3(if hefty:thin_join else: hair fi,0);
pos4(vair,90);
%pos5(curve,180);
%%
pos5(vair,180);
pos12(vair,180);
%%
pos6(vair,270);
penpos7(x3r-x3l,360);
lft x3l=min(lft x3l-(rt x3r-tiny.rt x2r),2/3[lft x2,edge]);
y3=bar_height;
x4l=.5(w-serif_fit)-.3u;
top y4r=x_height+oo;
lft x5r=hround max(1.35u-.5curve,.6u);
y5=.5x_height;
%%
y12=y5;
x12=x5+2.u;
%%
x6l=x4l-.2u;
bot y6r=vround 1/3vair;
lft x7l=edge;
y7=min(y3,y6+y4-y3+.6vair);
(x,y4r)=whatever[z3l,z4l];
x4r:=max(x,.5[x5r,x4]);
(x',y6r)=whatever[z7l,z6l];
x6r:=max(x',.5[x5r,x6]);
filldraw stroke z3e{up}...{left}z4e&super_arc.e(4,5)
 &super_arc.e(5,6)&z6e{right}...{up}z7e;  % bowl
filldraw stroke z3e{up}...{left}z4e&super_arc.e(4,12)
 &super_arc.e(12,6)&z6e{right}...{up}z7e;  % bowldd
y1=ypart(edge_path intersectionpoint(z3l{up}...{left}z4l));
y0=ypart(edge_path intersectionpoint(z7l{down}...{left}z6l));
pickup tiny.nib;
bot y2=if serifs: -.25d else: 0 fi;
filldraw stroke z1e--z0'e--z0e--z2e;  % stem
pickup crisp.nib;
pos8(hround(hair-stem_corr),0);
pos7'(stem',0);
z7'=z1;
x8r=x7'r;
top y8=h+oo;
filldraw stroke z7'e--z8e;  % point
if serifs: pickup tiny.nib;
 pos9(vair,-90);
 x9=.5[x2,x10];
 bot y9r=-d-o-1;
 pos10(hair,-180);
 lft x10r=hround u;
 y10=-.75d+.5flare;
 pos11(flare,-180);
 z11r=z10r;
 bulb(9,10,11); % bulb
 filldraw stroke super_arc.e(2,9);  % tail
else: pickup fine.nib;
pos2'(stem,0);
z2'=z2;
 z2''r=z2'r;
 z2''=z2';
 z2''l=(x2'l,0);
 pos9(vair,-90);
 x9=4.5u;
 bot y9r=-d-o-1;
 pos10(.5[vair,flare],-90);
 lft x10=hround 1.25u;
 y10r=good.y -5/6d;
 y10l:=good.y y10l;
 filldraw stroke z2'e..z2''e&super_arc.e(2'',9)
  & term.e(9,10,left,.9,4);
  fi  % tail
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12);
endchar;
